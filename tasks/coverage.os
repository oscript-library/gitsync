#Использовать fs
#Использовать coverage
#Использовать 1commands

Функция ЭтоWindows()
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ЭтоWindows = Найти(НРег(СистемнаяИнформация.ВерсияОС), "windows") > 0;
	Возврат ЭтоWindows;
КонецФункции

Процедура УстановитьДвижок(Команда)
	Команда.УстановитьКоманду("oscript");
	Если Не ЭтоWindows() Тогда
		Команда.ДобавитьПараметр("-encoding=utf-8");	
	КонецЕсли;
КонецПроцедуры

ФС.ОбеспечитьПустойКаталог("coverage");
ПутьКСтат = "coverage/stat.json";

Команда = Новый Команда;
УстановитьДвижок(Команда);
Команда.ДобавитьПараметр(СтрШаблон("-codestat=%1", ПутьКСтат));    
Команда.ДобавитьПараметр("tasks/test.os");
Команда.ПоказыватьВыводНемедленно(Истина);

КодВозврата = Команда.Исполнить();

Файл_Стат = Новый Файл(ПутьКСтат);

ИмяПакета = "gitsync";

ПроцессорГенерации = Новый ГенераторОтчетаПокрытия();

ПроцессорГенерации.ОтносительныеПути()
				.ИмяФайлаСтатистики()
				.GenericCoverage()
				.Cobertura()
				.Clover(ИмяПакета)
				.Сформировать();

ЗавершитьРаботу(КодВозврата);
