#Использовать logos

Перем ВерсияПлагина;
Перем Лог;
Перем Обработчик;
Перем МассивНомеровВерсий;

Функция Информация() Экспорт
	
	Возврат Новый Структура("Версия, Лог", ВерсияПлагина, Лог)
	
КонецФункции // Информация() Экспорт

Процедура ПриАктивизацииПлагина(СтандартныйОбработчик) Экспорт
	
	Обработчик = СтандартныйОбработчик;
	МассивНомеровВерсий = Неопределено;
КонецПроцедуры

Процедура ПослеПолученияТаблицыВерсий(ТаблицаИсторииХранилища, ПутьКХранилищу, КаталогРабочейКопии) Экспорт

	ПутьКФайлуСопоставления = ОбъединитьПути(КаталогРабочейКопии, Обработчик.ИмяФайлаАвторов());
	ТаблицаСопоставления = Обработчик.ПрочитатьФайлАвторовГитВТаблицуПользователей(ПутьКФайлуСопоставления);
	СоответствиеСообщенийОбОшибочныхАвторах = Новый Соответствие;
	
	МассивНомеровВерсий = новый Массив;

	Для Каждого Строка Из ТаблицаИсторииХранилища Цикл

		СтрокаПользователя = ТаблицаСопоставления.Найти(строка.Автор, "Автор");
		Если СтрокаПользователя = Неопределено Тогда
			
			МассивНомеровВерсий.Добавить(Строка.НомерВерсии);
				
		КонецЕсли;
	
	КонецЦикла;

КонецПроцедуры

Процедура ПередНачаломЦиклаОбработкиВерсий(ТаблицаИсторииХранилища, ТекущаяВерсия, СледующаяВерсия, МаксимальнаяВерсияДляРазбора) Экспорт
	
	КоличествоВерсий = 0;

	Для Каждого СтрокаВерсии из ТаблицаИсторииХранилища Цикл

		Если СтрокаВерсии.НомерВерсии < ТекущаяВерсия Тогда
			Продолжить;
		КонецЕсли;

		Если НЕ МассивНомеровВерсий.Найти(СтрокаВерсии.НомерВерсии) = Неопределено Тогда

			Лог.Отладка("Проверяю строку: "+ СтрокаВерсии.НомерВерсии);
			СтрокаОшибки = СтрШаблон("Нашли версию <%1>, а автор <%2> не сопоставлен пользователь git.", СтрокаВерсии.НомерВерсии, СтрокаВерсии.Автор);
			Лог.КритичнаяОшибка(СтрокаОшибки);
			КоличествоВерсий = КоличествоВерсий +1;
		КонецЕсли;
		
	КонецЦикла;

	Если КоличествоВерсий > 0  Тогда
		
		СтрокаОшибки = СтрШаблон("В таблице истории версий найдены авторы (количество %1), которые не сопоставлены в AUTHORS",КоличествоВерсий);
		
		ВызватьИсключение СтрокаОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт
	
	Возврат СтрШаблон("[PLUGIN] %1: %2 - %3", ИмяПлагина(), УровниЛога.НаименованиеУровня(Уровень), Сообщение);
	
КонецФункции

Функция ИмяПлагина()
	возврат "check-authors";
КонецФункции // ИмяПлагина()

Процедура Инициализация()

	ВерсияПлагина = "1.0.0";
	Лог = Логирование.ПолучитьЛог("oscript.app.gitsync.plugins."+ ИмяПлагина());
	КомандыПлагина = Новый Массив;
	КомандыПлагина.Добавить("sync");
	КомандыПлагина.Добавить("export");
	
	Лог.УстановитьРаскладку(ЭтотОбъект);

КонецПроцедуры

Инициализация();
