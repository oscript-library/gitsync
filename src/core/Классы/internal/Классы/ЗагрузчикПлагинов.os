#Использовать logos

Перем НаборПлагиновСинхронизации; // Соответствие 
Перем Лог; // Объект - Логирование
Перем ДоступныйИнтерфейсПлагинов; // Объект.ИнтерфейсОбъекта - интерфейс плагинов
Перем КаталогПлагинов; // Строка - каталог пакетов плагинов

#Область Экспортные_методы

// Устанавливает каталог плагинов
//
// Параметры:
//   ПутьККаталогу - Строка - полный путь к каталогу плагинов
//
Процедура УстановитьКаталогПлагинов(Знач ПутьККаталогу) Экспорт
	КаталогПлагинов = ПутьККаталогу;
КонецПроцедуры

// Выполняет загрузки плагинов в индекс
//
Процедура ЗагрузитьПлагины() Экспорт
	
	ПодключениеПлагиновКаталога = Новый ПодключениеПлагиновКаталога;
	МассивПлагинов = ПодключениеПлагиновКаталога.ПодключитьПлагиныКаталога(КаталогПлагинов);
	Лог.Отладка("Количество плагинов <%1> каталога <%2>", МассивПлагинов.Количество(), КаталогПлагинов);

	СоздательНабораПлагиновСинхронизации = Новый СоздательНабораПлагиновСинхронизации;

	Для каждого ИмяКлассаПлагина Из МассивПлагинов Цикл
		
		СоздательНабораПлагиновСинхронизации.ДобавитьПлагин(ИмяКлассаПлагина);

	КонецЦикла;

	ПодключениеЗапакованныхПлагинов = Новый ПодключениеЗапакованныхПлагинов;
	МассивПлагинов = ПодключениеЗапакованныхПлагинов.ИндексПлагинов();
	Лог.Отладка("Количество встроенных плагинов <%1>", МассивПлагинов.Количество());

	Для каждого ИмяКлассаПлагина Из МассивПлагинов Цикл
		
		СоздательНабораПлагиновСинхронизации.ДобавитьПлагин(ИмяКлассаПлагина);

	КонецЦикла;

	НаборПлагиновСинхронизации = СоздательНабораПлагиновСинхронизации.Создать();

КонецПроцедуры

// Возвращает загруженный индекс плагинов
//
//  Возвращаемое значение:
//   Соответствие - набор плагинов 
//                   * ключ - Строка - имя плагина
//                   * значение - Объект - ссылка на произвольный объект 
//
Функция ИндексПлагинов() Экспорт
	Возврат НаборПлагиновСинхронизации;
КонецФункции

#КонецОбласти

#Область Вспомогательные_процедуры_и_функции

Процедура ПриСозданииОбъекта(Знач ВходящийКаталогПлагинов = Неопределено)
	
	КаталогПлагинов = ВходящийКаталогПлагинов;
	НаборПлагиновСинхронизации = Новый Соответствие();
	Лог = Логирование.ПолучитьЛог("oscript.lib.gitsync.plugins.loader");

КонецПроцедуры

#КонецОбласти
