#Использовать logos

Перем МассивПодключенныхПлагинов;
Перем Лог;

Функция ПодключитьПлагиныКаталога(Знач КаталогПлагинов) Экспорт
		
	МассивПодключенныхПлагинов = Новый Массив();

	Если НРег(Лев(ТекущийСценарий().Источник, 10)) = "oscript://" Тогда
		Возврат МассивПодключенныхПлагинов;
	КонецЕсли;

	Лог.Отладка("Подключение плагинов из каталога <%1>", КаталогПлагинов);
	КаталогиПлагинов = НайтиФайлы(КаталогПлагинов, ПолучитьМаскуВсеФайлы(), Ложь); 

	Для каждого Каталоги Из КаталогиПлагинов Цикл
		
		Если Не Каталоги.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;
		
		ЗагрузитьПлагин(Каталоги.ПолноеИмя);

	КонецЦикла;

	Возврат МассивПодключенныхПлагинов;

КонецФункции

Процедура ЗагрузитьПлагин(Знач Путь)

	Лог.Отладка("Загружаю плагины из каталога <%1>", Путь);

	ФайлМанифеста = Новый Файл(ОбъединитьПути(Путь, "lib.config"));
	
	Если ФайлМанифеста.Существует() Тогда
		Лог.Отладка("Обрабатываем по манифесту");
		ДобавитьКлассыПлагинов(ФайлМанифеста.ПолноеИмя, Путь);
	Иначе
		Лог.Отладка("Плагины из каталога <%1> не могут быть загружены - не найден файл <lib.config>", Путь);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьКлассыПлагинов(Знач Файл, Знач Путь)
	
	Чтение = Новый ЧтениеXML;
	Чтение.ОткрытьФайл(Файл);
	Чтение.ПерейтиКСодержимому();
	
	Если Чтение.ЛокальноеИмя <> "package-def" Тогда
		Чтение.Закрыть();
		Возврат;
	КонецЕсли;
	
	Пока Чтение.Прочитать() Цикл
		
		Если Чтение.ТипУзла = ТипУзлаXML.Комментарий Тогда

			Продолжить;

		КонецЕсли;

		Если Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		
			Если Чтение.ЛокальноеИмя = "class" Тогда
				ФайлКласса = Новый Файл(ОбъединитьПути(Путь, Чтение.ЗначениеАтрибута("file")));
				Если ФайлКласса.Существует() 
					И ФайлКласса.ЭтоФайл() Тогда
					Идентификатор = Чтение.ЗначениеАтрибута("name");
					Если Не ПустаяСтрока(Идентификатор) Тогда
						ПодключитьСценарий(ФайлКласса.ПолноеИмя, Идентификатор);
						МассивПодключенныхПлагинов.Добавить(Идентификатор);									
					КонецЕсли;
				Иначе
					ВызватьИсключение "Не найден файл " + ФайлКласса.ПолноеИмя + ", указанный в манифесте";
				КонецЕсли;
				
				Чтение.Прочитать(); // в конец элемента

			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Чтение.Закрыть();
	
КонецПроцедуры

Лог = Логирование.ПолучитьЛог("oscript.lib.gitsync.plugins.loader");
МассивПодключенныхПлагинов = Новый Массив();