///////////////////////////////////////////////////////////////////
//
// Служебный модуль с набором методов работы с командами приложения
//
// Представляет собой модификацию приложения gitsync от 
// команды oscript-library
//
// Идея формата модуля взята из проекта oscript-template
//
///////////////////////////////////////////////////////////////////

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт

	ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Вывод справки по параметрам");
	Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "Команда");
	Парсер.ДобавитьКоманду(ОписаниеКоманды);
	
КонецПроцедуры // ЗарегистрироватьКоманду

Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Неиспользуется = Неопределено) Экспорт
    
	Парсер = Новый ПарсерАргументовКоманднойСтроки;

	МенеджерКомандПриложения.ЗарегистрироватьКоманды(Парсер);

	Если ПараметрыКоманды["Команда"] = Неопределено Тогда

		ПоказатьВозможныеКоманды(Парсер);

	Иначе
		
		ПоказатьСправкуПоКоманде(Парсер, ПараметрыКоманды["Команда"]);

	КонецЕсли;

	Возврат МенеджерКомандПриложения.РезультатыКоманд().Успех;

КонецФункции // ВыполнитьКоманду

Процедура ПоказатьВозможныеКоманды(Знач Парсер)
	
	ВозможныеКоманды = Парсер.СправкаВозможныеКоманды();
	Сообщить(" ");
	Сообщить("Возможные команды:");
	
	МаксШирина = 0;
	Для Каждого Команда Из ВозможныеКоманды Цикл
		
		МаксШирина = Макс(МаксШирина, СтрДлина(Команда.Команда));
		
	КонецЦикла;
	
	Поле = "               ";	
	Для Каждого Команда Из ВозможныеКоманды Цикл

		Сообщить(" " + Лев(Команда.Команда + Поле, МаксШирина + 2) + "- " + Команда.Пояснение);

	КонецЦикла;

	Сообщить(" ");
	Сообщить("Для подсказки по конкретной команде наберите help <команда>");

КонецПроцедуры // ПоказатьВозможныеКоманды

Процедура ПоказатьСправкуПоКоманде(Знач Парсер, Знач ИмяКоманды)

	ВозможныеКоманды = Парсер.СправкаВозможныеКоманды();
	ОписаниеКоманды = ВозможныеКоманды.Найти(ИмяКоманды, "Команда");
	Если ОписаниеКоманды = Неопределено Тогда

		Сообщить("Команда отсуствует: " + ИмяКоманды);
		Возврат;

	КонецЕсли;
	
	Сообщить("" + ОписаниеКоманды.Команда + " - " + ОписаниеКоманды.Пояснение);
	ВывестиПараметры(ОписаниеКоманды.Параметры);

КонецПроцедуры // ПоказатьСправкуПоКоманде

Процедура ВывестиПараметры(Знач ОписаниеПараметров)
	
	Сообщить("Параметры:");
	Для Каждого СтрПараметр Из ОписаниеПараметров Цикл

		Если Не СтрПараметр.ЭтоИменованныйПараметр Тогда

			Сообщить(СтрШаблон(" <%1> - %2", СтрПараметр.Имя, СтрПараметр.Пояснение));

		Иначе
			
			Сообщить(СтрШаблон(" %1 - %2", СтрПараметр.Имя, СтрПараметр.Пояснение));

		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры // ВывестиПараметры
