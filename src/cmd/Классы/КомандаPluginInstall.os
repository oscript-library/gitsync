#Использовать "../../core"

Перем Лог;

Процедура ОписаниеКоманды(Команда) Экспорт
	
	Команда.Опция("f file", Новый Массив, "Путь к плагину ( *.ospx)")
				.ТМассивСтрок()
				.ВОкружении("GITSYNC_INSTALL_PATH");
	Команда.Аргумент("NAME", Новый Массив, "Имя пакета вида <ИмяПакета>@1.0.0>")
				.ТМассивСтрок()
				.ВОкружении("GITSYNC_INSTALL_PACKAGES");

	Команда.Спек = "(-f... | NAME...)";

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Если ПараметрыПриложения.ЭтоСборкаEXE() Тогда
		
		Лог.Информация("Установка новых плагинов для собранного приложения в exe не доступна");

		Возврат;

	КонецЕсли;


	ПакетыКУстановке = Команда.ЗначениеАргумента("NAME");
	МассивФайловПлагинов = Команда.ЗначениеОпции("file");

	МенеджерПлагинов = ПараметрыПриложения.МенеджерПлагинов();
	
	Для каждого ПутьКПлагину Из МассивФайловПлагинов Цикл

		Лог.Отладка("Устанавливаю плагин из файла: <%1>", ПутьКПлагину);
		МенеджерПлагинов.УстановитьФайлПлагин(ПутьКПлагину);

	КонецЦикла;

	Для каждого ИмяПакета Из ПакетыКУстановке Цикл
		
		МенеджерПлагинов.УстановитьПлагинПоИмени(ИмяПакета);

	КонецЦикла;

КонецПроцедуры

Лог = ПараметрыПриложения.Лог();