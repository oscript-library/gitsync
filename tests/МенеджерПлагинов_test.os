#использовать "../src/core/"
#Использовать asserts
#Использовать logos

Перем Лог;
Перем КаталогПлагинов;

Функция ПолучитьСписокТестов(Знач Тестирование) Экспорт

	ИменаТестов = Новый Массив;
	
	ИменаТестов.Добавить("ТестДолжен_ПроверитьИндексПлагинов");
	ИменаТестов.Добавить("ТестДолжен_ПроверитьВключениеПлагинов");
	ИменаТестов.Добавить("ТестДолжен_ПроверитьВыключениеПлагинов");
	
	Возврат ИменаТестов;

КонецФункции

Процедура ПередЗапускомТестов() Экспорт

	КаталогПлагинов = ВременныеФайлы.СоздатьКаталог();
	КаталогФич = ОбъединитьПути(КаталогПроекта(), "features", "core");
	КаталогТестовогоПлагина = ОбъединитьПути(КаталогФич, "step_definitions", "testsata", "test_plugin");
	
	ФС.КопироватьСодержимоеКаталога(КаталогТестовогоПлагина, КаталогПлагинов);

	Команда = Новый Команда;
	Команда.УстановитьКоманду("opm");
	Команда.ДобавитьПараметр("build");
	Команда.ДобавитьПараметр("--out");
	Команда.ДобавитьПараметр(КаталогПлагинов);
	Команда.ДобавитьПараметр(КаталогПлагинов);
	КодВозврата = Команда.Исполнить();

	Лог.Информация(Команда.ПолучитьВывод());

	Если Не КодВозврата = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания тестового плагина", Команда.ПолучитьВывод());
	КонецЕсли;

	МассивФайлов = НайтиФайлы(КаталогПлагинов, "*.ospx");

	Если МассивФайлов.Количество() = 0 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке("Ошибка создания тестового плагина", "Не найден собранный файл плагина");
	КонецЕсли;

КонецПроцедуры

Процедура ТестДолжен_ПроверитьИндексПлагинов() Экспорт

	МенеджерПлагинов = Новый МенеджерПлагинов(КаталогПлагинов);
	МенеджерПлагинов.УстановитьФайлПлагин("test_plugin-0.0.1.ospx");
	МенеджерПлагинов.ЗагрузитьПлагины();

	ИндексПлагинов = МенеджерПлагинов.ПолучитьИндексПлагинов();
	Ожидаем.Что(ИндексПлагинов.Количество(), "Количество плагинов должно быть равно").Равно(1);

КонецПроцедуры

Процедура ТестДолжен_ПроверитьВключениеПлагинов() Экспорт
	
	МенеджерПлагинов = Новый МенеджерПлагинов(КаталогПлагинов);
	МенеджерПлагинов.УстановитьФайлПлагин("test_plugin-0.0.1.ospx");
	МенеджерПлагинов.ЗагрузитьПлагины();

	ИндексПлагинов = МенеджерПлагинов.ПолучитьИндексПлагинов();
	Ожидаем.Что(ИндексПлагинов.Количество(), "Количество плагинов должно быть равно").Равно(1);

	МенеджерПлагинов.ВключитьПлагин("test_plugin");

	Ожидаем.Что(ИндексПлагинов["test_plugin"].Включен(), "Плагин должен быть включен").ЭтоИстина();

КонецПроцедуры

Процедура ТестДолжен_ПроверитьВыключениеПлагинов() Экспорт
	
	МенеджерПлагинов = Новый МенеджерПлагинов(КаталогПлагинов);
	МенеджерПлагинов.УстановитьФайлПлагин("test_plugin-0.0.1.ospx");
	МенеджерПлагинов.ЗагрузитьПлагины();

	ИндексПлагинов = МенеджерПлагинов.ПолучитьИндексПлагинов();
	Ожидаем.Что(ИндексПлагинов.Количество(), "Количество плагинов должно быть равны").Равно(1);

	МенеджерПлагинов.ВключитьПлагин("test_plugin");

	Ожидаем.Что(ИндексПлагинов["test_plugin"].Включен(), "Плагин должен быть включен").ЭтоИстина();

	МассивПлагинов = Новый Массив();
	МассивПлагинов.Добавить("test_plugin");

	МенеджерПлагинов.ОтключитьПлагины(МассивПлагинов);

	Ожидаем.Что(ИндексПлагинов["test_plugin"].Включен(), "Плагин должен быть выключен").ЭтоЛожь();

КонецПроцедуры

Процедура ПослеЗапускаТестов() Экспорт

	ВременныеФайлы.УдалитьФайл(КаталогПлагинов);

КонецПроцедуры

Функция КаталогПроекта()
	
	ПутьККаталогуПроекта = ОбъединитьПути(ТекущийСценарий().Каталог, "..");
	Файл = Новый Файл(ПутьККаталогуПроекта);
	Возврат Файл.ПолноеИмя;

КонецФункции

Лог = Логирование.ПолучитьЛог("oscript.lib.gitsync");
